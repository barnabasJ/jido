defmodule Jido.Pod do
  @moduledoc """
  Pod wrapper macro and runtime helpers.

  A pod is just a `Jido.Agent` with a canonical topology and a singleton pod
  slice mounted under the `:pod` slice key.
  """

  alias Jido.Agent
  alias Jido.Agent.InstanceManager
  alias Jido.AgentServer
  alias Jido.AgentServer.State
  alias Jido.Plugin.Instance, as: PluginInstance
  alias Jido.Pod.Definition
  alias Jido.Pod.Mutable
  alias Jido.Pod.Mutation
  alias Jido.Pod.Mutation.Report
  alias Jido.Pod.Runtime
  alias Jido.Pod.Topology
  alias Jido.Pod.Topology.Node
  alias Jido.Pod.TopologyState

  @type node_status :: :adopted | :running | :misplaced | :stopped
  @type ensure_source :: :adopted | :running | :started
  @type node_name :: Topology.node_name()

  @type node_snapshot :: %{
          node: Node.t(),
          key: term(),
          pid: pid() | nil,
          running_pid: pid() | nil,
          adopted_pid: pid() | nil,
          owner: node_name() | nil,
          expected_parent: map(),
          actual_parent: map() | nil,
          adopted?: boolean(),
          status: node_status()
        }

  @type ensure_result :: %{
          pid: pid(),
          source: ensure_source(),
          owner: node_name() | nil,
          parent: :pod | node_name()
        }

  @type reconcile_report :: %{
          requested: [node_name()],
          waves: [[node_name()]],
          nodes: %{node_name() => ensure_result()},
          failures: %{node_name() => term()},
          completed: [node_name()],
          failed: [node_name()],
          pending: [node_name()]
        }

  @type mutation_report :: Report.t()

  defmacro __using__(opts) do
    name = Definition.expand_and_eval_literal_option(Keyword.fetch!(opts, :name), __CALLER__)
    raw_topology = Keyword.get(opts, :topology, %{})
    topology = Definition.resolve_topology!(name, raw_topology, __CALLER__)

    default_plugins =
      Definition.expand_and_eval_literal_option(Keyword.get(opts, :default_plugins), __CALLER__)

    {pod_plugins, remaining_default_plugins} =
      Definition.split_pod_plugins!(default_plugins, __CALLER__)

    user_plugins =
      Definition.expand_and_eval_literal_option(Keyword.get(opts, :plugins, []), __CALLER__)

    agent_opts =
      opts
      |> Keyword.delete(:topology)
      |> Keyword.put(:plugins, pod_plugins ++ (user_plugins || []))
      |> Keyword.put_new(:path, :app)
      |> then(fn resolved_opts ->
        if is_nil(remaining_default_plugins) do
          Keyword.delete(resolved_opts, :default_plugins)
        else
          Keyword.put(resolved_opts, :default_plugins, remaining_default_plugins)
        end
      end)

    quote location: :keep do
      use Jido.Agent, unquote(Macro.escape(agent_opts))

      @pod_topology unquote(Macro.escape(topology))

      @doc "Returns the canonical topology for this pod agent."
      @spec topology() :: Jido.Pod.Topology.t()
      def topology, do: @pod_topology

      @doc "Returns true for pod-wrapped agent modules."
      @spec pod?() :: true
      def pod?, do: true

      defoverridable new: 1

      @doc """
      Pod-wrapped `new/1`. Seeds the `:pod` slice with the agent module's
      canonical topology before delegating to the base `Jido.Agent.new/1`. User
      state at `state: %{pod: %{...}}` shallow-overrides the topology fields.
      """
      def new(opts \\ []) do
        opts_map = if is_list(opts), do: Map.new(opts), else: opts
        user_state = Map.get(opts_map, :state, %{})

        pod_seed = %{topology: topology(), topology_version: topology().version}

        existing_pod = Map.get(user_state, :pod, %{})
        new_pod = Map.merge(pod_seed, existing_pod)
        new_state = Map.put(user_state, :pod, new_pod)

        opts_with_pod_state =
          opts_map
          |> Map.put(:state, new_state)
          |> Map.to_list()

        super(opts_with_pod_state)
      end
    end
  end

  @doc """
  Gets a pod instance through the given `InstanceManager` and immediately
  reconciles eager nodes.

  This is the default happy path for pod lifecycle access. Call
  `Jido.Agent.InstanceManager.get/3` directly if you need lower-level control
  over reconciliation timing.
  """
  @spec get(atom(), term(), keyword()) :: {:ok, pid()} | {:error, term()}
  def get(manager, key, opts \\ []) when is_atom(manager) and is_list(opts) do
    with {:ok, pod_pid} <- InstanceManager.get(manager, key, opts),
         :ok <- AgentServer.await_ready(pod_pid) do
      case reconcile(pod_pid) do
        {:ok, _started} ->
          {:ok, pod_pid}

        {:error, reason} ->
          {:error, %{stage: :reconcile, pod: pod_pid, reason: reason}}
      end
    end
  end

  @doc """
  Returns the reserved pod plugin instance for a pod-wrapped agent module.
  """
  @spec pod_plugin_instance(module()) :: {:ok, PluginInstance.t()} | {:error, term()}
  defdelegate pod_plugin_instance(agent_module), to: TopologyState

  @doc """
  Fetches pod plugin state from an agent or server state.
  """
  @spec fetch_state(Agent.t() | State.t()) :: {:ok, map()} | {:error, term()}
  defdelegate fetch_state(agent_or_state), to: TopologyState

  @doc """
  Fetches the canonical topology from a module, agent, or running pod server.
  """
  @spec fetch_topology(module() | Agent.t() | State.t() | AgentServer.server()) ::
          {:ok, Topology.t()} | {:error, term()}
  defdelegate fetch_topology(source), to: TopologyState

  @doc """
  Replaces the persisted topology snapshot in a pod agent.

  Structural topology changes advance `topology.version`; no-op replacements
  preserve the current version.
  """
  @spec put_topology(Agent.t(), Topology.t()) :: {:ok, Agent.t()} | {:error, term()}
  defdelegate put_topology(agent, topology), to: TopologyState

  @doc """
  Applies a pure topology transformation to a pod agent.

  Structural topology changes advance `topology.version`; no-op updates preserve
  the current version.
  """
  @spec update_topology(
          Agent.t(),
          (Topology.t() -> Topology.t() | {:ok, Topology.t()} | {:error, term()})
        ) ::
          {:ok, Agent.t()} | {:error, term()}
  defdelegate update_topology(agent, fun), to: TopologyState

  @doc """
  Submits a topology mutation to a running pod and returns immediately with
  a queued ack — does **not** wait for the mutation to complete.

  Returns `{:ok, %{mutation_id: id, queued: true}}` once the action's slice
  return has set the pod's mutation slice. The action's
  `ensure_mutation_idle/1` rejection (`{:error, :mutation_in_progress}`) and
  any planner validation errors (`{:error, %Jido.Error{}}`) are delivered
  through the framework's tagged-tuple ack per
  [ADR 0018](../../guides/adr/0018-tagged-tuple-return-shape.md) §3 — callers
  do not encode failure branches in the default selector.

  For the post-completion mutation report, use `mutate_and_wait/3`.

  `server` follows the same resolution rules as `Jido.AgentServer.state/3` and
  `Jido.AgentServer.call/4`. Pass the running pod pid, a locally registered
  server name, or another resolvable runtime server reference. Raw string ids
  still require explicit registry lookup before use.

  ## Options

  - `:await_timeout` (or `:timeout`) — ms to wait for the queued ack
    (default `:timer.seconds(30)`).
  - `:selector` — override the default queued-ack selector. Power users only;
    most callers want the default `%{mutation_id: id, queued: true}` projection.
  """
  @spec mutate(AgentServer.server(), [Mutation.t() | term()], keyword()) ::
          {:ok, %{mutation_id: String.t(), queued: true}} | {:error, term()}
  defdelegate mutate(server, ops, opts \\ []), to: Mutable

  @doc """
  Submits a topology mutation and waits for the mutation slice to reach a
  terminal status, returning the completion report (or failure error).

  Wraps `mutate/3` with the subscribe-before-cast pattern from
  [ADR 0017](../../guides/adr/0017-pod-mutations-are-signal-driven.md):
  subscribes to `jido.agent.child.*` with a selector that reads
  `pod.mutation` and matches by `mutation.id` **before** issuing the cast.
  The state machine flips `mutation.status` to `:completed`/`:failed` when
  the last awaited child
  lifecycle signal arrives; the subscriber's selector fires on that same
  signal and delivers the terminal report. There is no synthetic
  `jido.pod.mutate.completed`/`.failed` signal — the slice is the
  contract.

  Returns `{:ok, mutation_report()}` on success or `{:error, term()}` on
  any failure path (action rejection, planner error, runtime materialization
  failure, or `:timeout`).
  """
  @spec mutate_and_wait(AgentServer.server(), [Mutation.t() | term()], keyword()) ::
          {:ok, mutation_report()} | {:error, term()}
  defdelegate mutate_and_wait(server, ops, opts \\ []), to: Mutable

  @doc """
  Builds the new pod slice value and runtime side-effect directives for an
  in-turn pod mutation.
  """
  @spec mutation_effects(Agent.t(), [Mutation.t() | term()], keyword()) ::
          {:ok, map(), [struct()]} | {:error, term()}
  defdelegate mutation_effects(agent, ops, opts \\ []), to: Mutable

  @doc """
  Returns runtime snapshots for every node in a running pod.
  """
  @spec nodes(AgentServer.server()) :: {:ok, %{node_name() => node_snapshot()}} | {:error, term()}
  defdelegate nodes(server), to: Runtime

  @doc """
  Looks up a node's live process if it is currently running.
  """
  @spec lookup_node(AgentServer.server(), node_name()) :: {:ok, pid()} | :error | {:error, term()}
  defdelegate lookup_node(server, name), to: Runtime

  @doc """
  Ensures a named node is running and adopted into the pod manager.
  """
  @spec ensure_node(AgentServer.server(), node_name(), keyword()) ::
          {:ok, pid()} | {:error, term()}
  defdelegate ensure_node(server, name, opts \\ []), to: Runtime

  @doc """
  Ensures all eager nodes are running and adopted into the pod manager.
  """
  @spec reconcile(AgentServer.server(), keyword()) ::
          {:ok, reconcile_report()} | {:error, reconcile_report()}
  defdelegate reconcile(server, opts \\ []), to: Runtime
end
